?start: program

program: function_decl*

function_decl: EXTERN? type NAME "(" param_list? ")" block

param_list: param ("," param)*
param: type NAME

type: "int" | "float" | "bool" | "string" | "point" | "object" | "void"

block: "{" statement* "}"

statement: block
         | var_decl ";"
         | assignment ";"
         | if_stmt
         | while_stmt
         | return_stmt ";"
         | expr ";"

var_decl: type NAME ("=" expr)?
assignment: lvalue "=" expr
lvalue: NAME | NAME "." NAME

if_stmt: "if" "(" expr ")" statement ("else" statement)?
while_stmt: "while" "(" expr ")" statement
return_stmt: "return" expr?

?expr: logic_or
?logic_or: logic_and ("||" logic_and)*
?logic_and: equality ("&&" equality)*
?equality: relation (("=="|"!=") relation)*
?relation: sum (("<"|"<="|">"|">=") sum)*
?sum: term (("+"|"-") term)*
?term: factor (("*"|"/"|"%") factor)*
?factor: "!" factor
       | "-" factor
       | atom

?atom: literal
     | NAME
     | NAME "(" arg_list? ")"
     | "(" expr ")"
     | NAME "." NAME

arg_list: expr ("," expr)*

literal: INT | FLOAT | BOOL | STRING | NAME

EXTERN: "extern"
BOOL: "true" | "false"

%import common.INT
%import common.FLOAT
%import common.ESCAPED_STRING -> STRING
%import common.CNAME -> NAME
%import common.WS
%ignore WS
